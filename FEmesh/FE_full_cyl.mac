fini
/clear

/INQUIRE,directory,DIRECTORY        !Save current working directory in stringarry
                                    !From Batch file working directory defined as ....\00_AOAD

dir=directory(1,1,1)                ! Create character variable with value of string arry
! dir='D:\diss_jf_mat\00_AOAD'

! /CWD,'%dir%\Ansys_WD' !Define actual Working directory as \00_AOAD\Ansys_WD
/CWD,'%dir%' !Define actual Working directory as \Ansys_wd

!********************************************************************
! Import variables 
!********************************************************************

!------------------------------------------------
! Makro 1: Radius of Cylinder and discretization
!------------------------------------------------
PI = ACOS(-1)

*set,radius_input
*dim,radius_input, array, 1

*create,macro1
*VREAD,radius_input(1),'%dir%\radius_export',txt,,,0
(F10.0)
*END
*USE,macro1

radius=radius_input(1)

! Number of elements per half radius
*set,div_input
*dim,div_input, array, 1

*create,macro2
*VREAD,div_input(1),'%dir%\div_export',txt,,,0
(F10.0)
*END
*USE,macro2

div=div_input(1)

fak=1.0

!********************************************************************
! Preprosessing
!********************************************************************
!---------
/prep7 
!---------

! Keypoints
K,1,0,0,0
K,2,0,radius/2,0
K,3,0,0,radius/2
K,4,0,-radius/2,0
K,5,0,0,-radius/2
K,6,0,fak*radius/2,fak*radius/2
K,7,0,-fak*radius/2,fak*radius/2
K,8,0,-fak*radius/2,-fak*radius/2
K,9,0,fak*radius/2,-fak*radius/2

K,10,1,0,0

CIRCLE,1,radius,10,2
lsel,all
LDIV,all,0.5, ,2,0

! Areas
A,12,3,6,15
A,3,1,2,6
A,15,6,2,11
A,11,2,9,18
A,2,1,5,9
A,18,9,5,14
A,14,5,8,17
A,5,1,4,8
A,17,8,4,13
A,13,4,7,16
A,4,1,3,7
A,16,7,3,12


! Lines
L,12,3,div
L,3,1,div
L,1,2,div
L,2,11,div
L,1,5,div
L,5,14,div
L,1,4,div
L,4,13,div

! Mesh
ET,1,SHELL181
MSHKEY,1
Asel,all
AMESH,all


!--------------------------------------------------------------------
! Export Node and Element Lists
!--------------------------------------------------------------------

! Nodes
*GET,NUM_NODE,NODE,0,COUNT

*create,macro3
*CFOPEN,'%dir%\Knoten_import',dat

*DO,i,1,NUM_NODE,1
*VWRITE,i,NX(i),NY(i),NZ(i)
%4I %22G %22G %22G
*ENDDO

*CFCLOS
*END
*USE,macro3


! Elements
*GET,NUM_ELEM,ELEM,0,COUNT

*create,macro4
*CFOPEN,'%dir%\Elemente_import',dat

*DO,i,1,NUM_ELEM,1
*VWRITE,i,NELEM(i,1),NELEM(i,2),NELEM(i,3),NELEM(i,4)
%4I %22I %22I %22I %22I
*ENDDO

*CFCLOS
*END
*USE,macro4