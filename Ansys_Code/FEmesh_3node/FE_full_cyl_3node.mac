! ====================================================================
!  Project:  Cylinder with inner square – 3-node triangle mesh (SHELL181)
!  Purpose:  Build FE mesh with triangular elements via degeneration
!            (free meshing; triangle-only), then export nodes & elements
!  Author:   Boenisch      Date: 27.08.2025
! ====================================================================


!====================== CLEAN START & Working directory ======================
fini        ! close active processor
/clear      ! clear database (geometry/mesh) -> clean state
/PREP7      ! enter preprocessor (define geometry, elements, meshing)

/INQUIRE,directory,DIRECTORY    ! query current working dir into array 'directory'
*SET,dir,directory(1,1,1)       ! turn array entry into string variable 'dir'
/CWD,'%dir%'                    ! set current Working Directory


!------------------------------------------------
! Makro 1: Radius of Cylinder and discretization
!------------------------------------------------
PI = ACOS(-1)                   ! define pi

! --- Read radius from text file (Matlab-produced) into array, then scalar ---
*set,radius_input
*dim,radius_input, array, 1

*create,macro1
*VREAD,radius_input(1),'%dir%\radius_export',txt,,,0
(F10.0)
*END
*USE,macro1

radius=radius_input(1)      !scalar radius now available

! --- Read discretization parameter 'div' per half radius ---
*set,div_input
*dim,div_input, array, 1

*create,macro2
*VREAD,div_input(1),'%dir%\div_export',txt,,,0
(F10.0)
*END
*USE,macro2

div=div_input(1)            !'div' controls mesh density later (via ESIZE)

fak=1.0

!------------------------------------------------
! Preprocessing (geometry creation)
!------------------------------------------------

/prep7

! ------------------------ KEYPOINTS -----------------------------
! All points are placed in the YZ-plane (x=0); KP10 defines circle plane normal
K,1,0,0,0                          ! Center
K,2,0,radius/2,0                   ! +Y/2
K,3,0,0,radius/2                   ! +Z/2
K,4,0,-radius/2,0                  ! -Y/2
K,5,0,0,-radius/2                  ! -Z/2
K,6,0,fak*radius/2,fak*radius/2    ! Inner square corner (diag +Y/+Z)
K,7,0,-fak*radius/2,fak*radius/2   ! Inner square corner (diag -Y/+Z)
K,8,0,-fak*radius/2,-fak*radius/2  ! Inner square corner (diag -Y/-Z)
K,9,0,fak*radius/2,-fak*radius/2   ! Inner square corner (diag +Y/-Z)

K,10,1,0,0                         ! Normal-defining point (circle plane)


! ------------------------ OUTER CIRCLE --------------------------
CIRCLE,1,radius,10,2               ! Full circle: center=KP1, R=radius, normal via KP10, 2 half-arcs
lsel,all
LDIV,all,0.5, ,2,0                 ! (Legacy) line division setting; OK to leave, ESIZE governs free mesh

! ------------------------ AREAS (12) --------------------
! Ring of mapped-like areas between inner square and outer circle
A,12,3,6,15
A,3,1,2,6
A,15,6,2,11
A,11,2,9,18
A,2,1,5,9
A,18,9,5,14
A,14,5,8,17
A,5,1,4,8
A,17,8,4,13
A,13,4,7,16
A,4,1,3,7
A,16,7,3,12

! ------------------------ LINES -----------------------        Boenisch changed
L,12,3,div
L,3,1,div
L,1,2,div
L,2,11,div
L,1,5,div
L,5,14,div
L,1,4,div
L,4,13,div
L,12,3,div
L,3,1,div
L,1,2,div
L,2,11,div
L,1,5,div
L,5,14,div
L,1,4,div
L,4,13,div

! ------------------------ ELEMENT TYPE & MESH -------------------           Boenisch changed
ET,1,SHELL181                       ! 4-node shell element (degenerates to 3-node triangles)                          
MSHKEY,0                            ! Free meshing (NOT mapped because of the ´0´)
MSHAPE,1,2D                         ! 2D shape selection (triangle-only in this setup/version)
ESIZE,radius/(2*div)                ! Target edge length ≈ (radius/2)/div
Asel,all
AMESH,all                           ! Mesh all selected areas

!-----------------------------------------------------------------
! EXPORT: NODE AND ELEMENT LISTS (for MATLAB post-processing)
!-----------------------------------------------------------------

! ------------------------ NODES EXPORT --------------------------
*GET,NUM_NODE,NODE,0,COUNT

*create,macro3
*CFOPEN,'%dir%\Knoten_import',dat       ! Open node output file in wd

*DO,i,1,NUM_NODE,1
*VWRITE,i,NX(i),NY(i),NZ(i)             ! Write: node id, x, y, z
%4I %22G %22G %22G
*ENDDO

*CFCLOS
*END
*USE,macro3

! ------------------------ ELEMENTS EXPORT -----------------------

*GET,NUM_ELEM,ELEM,0,COUNT              ! Number of FE elements

*create,macro4
*CFOPEN,'%dir%\Elemente_import',dat

*DO,i,1,NUM_ELEM,1
*VWRITE,i,NELEM(i,1),NELEM(i,2),NELEM(i,3),NELEM(i,4)       ! Element id + up to 4 node ids
%4I %22I %22I %22I %22I
*ENDDO

*CFCLOS
*END
*USE,macro4
